/*
 * This code is part of Freenet. It is distributed under the GNU General
 * Public License, version 2 (or at your option any later version). See
 * http://www.gnu.org/ for further details of the GPL.
 */

buildscript {
    dependencies {
        apply from: 'dependencies.gradle'
    }
}

plugins {
    id "java"
    id "maven-publish"
    id "com.github.spotbugs"     version "4.0.1"
    id "org.sonarqube"           version "2.8"
    id "com.adarshr.test-logger" version "2.0.0"
    id "org.ajoberstar.grgit"    version "4.0.2"
}

repositories {
    mavenCentral()
    jcenter()
    exclusiveContent {
        forRepository {
            maven {
                url "https://mvn.freenetproject.org"
            }
        }
        filter {
            includeGroup "org.freenetproject"
        }
    }
}

dependencies {
    moduleDependencies.each {
        add(it.config, it.dep, it.options)
    }
}

sourceSets {
    main.java.srcDirs = ['src']
    test.java.srcDirs = ['test']
}

// Configuration
apply from: "$rootDir/gradle/analysis.gradle"

// output: build01484-13-g5831bbf
String gitrev = grgit.open(dir: project.rootDir).describe()
// output: 11.0.3 amd64 (Oracle Corporation)
String buildenv = "${System.properties['java.version']} ${System.properties['os.arch']}"
// true if running on a build server
boolean isGithub = System.getenv("GITHUB_CI")
boolean isCiServer = System.getenv("CI")
// true if running on $OS
boolean isWindows = org.gradle.internal.os.OperatingSystem.current().isWindows()
boolean isOSX = org.gradle.internal.os.OperatingSystem.current().isMacOsX()

tasks.withType(JavaCompile) {
    //options.compilerArgs += ['-Xlint:unchecked']
    sourceCompatibility = JavaVersion.toVersion(project.findProperty('targetJavaVersion')?:8)
    targetCompatibility = sourceCompatibility
    options.encoding = 'UTF-8'

    // for testing with an unsupported jdk
    // fork compile jobs to another compiler than the one its running with
    if (project.hasProperty('javaHome'))
        options.with {
            fork = true
            forkOptions.javaHome = file(project.findProperty('javaHome')?:null)
            forkOptions.executable = "$forkOptions.javaHome/bin/javac"
        }
}
tasks.withType(Javadoc) {
    // turn off overly pedantic lint checking
    options.addStringOption('Xdoclint:none', '-quiet')
    options.encoding = 'UTF-8'
}
tasks.withType(AbstractArchiveTask) {
    preserveFileTimestamps = false
    reproducibleFileOrder  = true
}

// Tasks

tasks.register('compileVersion', JavaCompile) {
    copy {
        from sourceSets.main.java.srcDirs
        into temporaryDir
        include 'freenet/node/Version.java'
        filter {
            String line -> line.replaceAll("@custom@","${gitrev}")
        }
    }
    source = fileTree(dir: temporaryDir, include: '**/*.java')
    classpath = files(sourceSets.main.output.classesDirs)
    destinationDir = sourceSets.main.output.classesDirs[0]
}

processResources {
    from(sourceSets.main.java.srcDirs) {
        include([
            'freenet/l10n/*properties',
            'freenet/l10n/iso-*.tab',
            'freenet/clients/http/staticfiles/**',
        ])
    }
    from(projectDir) {
        include 'dependencies.properties'
    }
}
processTestResources {
    from(sourceSets.test.java.srcDirs) {
        include([
            'freenet/client/filter/*/**',
            'freenet/crypt/ciphers/rijndael-gladman-test-data/**',
            'freenet/l10n/*properties',
        ])
    }
}

tasks.register('copyRuntimeLibs', Copy) {
    dependsOn 'jar'
    into "${buildDir}/output"
    from configurations.runtimeClasspath
    from jar
}

tasks.register('tar', Tar) {
    compression = Compression.BZIP2
    String archiveName = 'freenet-sources.tar.bz2'
    archiveFileName = archiveName
    destinationDirectory = buildDir

    from(projectDir) {
        exclude([
            'build',
            '.git',
            '.gradle',
        ])
    }
    doLast {
        ant.checksum(file:"$buildDir/$archiveName",
                     algorithm:'sha-256',
                     fileext:'.sha256')
    }
}

jar {
    dependsOn 'compileVersion'
    archivesBaseName = 'freenet'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes([
          "Permissions": "all-permissions",
          "Application-Name": "Freenet REference Daemon",
          "Required-Ext-Version": 29,
          "Recommended-Ext-Version": 29,
          "Compiled-With": "${buildenv}",
        ])
        attributes([
          "Specification-Title": "Freenet",
          "Specification-Version": "0.7.5",
          "Specification-Vendor": "freenetproject.org",
          "Implementation-Title": "Freenet",
          "Implementation-Version": "0.7.5 ${gitrev}",
          "Implementation-Vendor": "freenetproject.org",
        ], "common")
    }

    doLast {
        ant.checksum(file: "${buildDir}/libs/freenet.jar",
                     algorithm:'sha-256', property:'hash')
        println "SHA-256 of freenet.jar: ${ant.properties['hash']}"
        println "${gitrev} compiled with ${buildenv}"
    }
}

test {
    useJUnitPlatform()
    ignoreFailures = false

    reports {
        html.enabled = true
        html.destination = file("${buildDir}/reports/junit")
    }

    testlogger {
        theme 'standard-parallel'
        slowThreshold = 10000
        showFailedStandardStreams = true
    }

    if ((findProperty('parallelTests')?:'false') == 'true')
        maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 2

    workingDir sourceSets.test.output.resourcesDir
    include 'freenet/**/*Test.class'
    exclude 'freenet/**/*$*Test.class'

    // TODO: M3UFilterTest fails only on github/windows
    // if (isGithub && isWindows)
    //      exclude(['freenet/client/filter/M3UFilterTest*'])

    systemProperties += [
        "test.l10npath_test": "freenet/l10n/",
        "test.l10npath_main": "../main/freenet/l10n/",
    ]
}

// Release Publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'org.freenetproject'
            artifactId "fred"
            version gitrev
            from components.java
        }
    }
    repositories {
        maven {
            url "s3://mvn.freenetproject.org/"
            credentials(AwsCredentials) {
                accessKey System.getenv('AWS_ACCESS_KEY_ID')
                secretKey System.getenv('AWS_SECRET_ACCESS_KEY')
            }
        }
    }
}
